*** Settings ***
Library ExtendedSelenium2Library


*** Variables ***
${Browser}          Firefox
${Chrome}           chrome
${SiteUrl}          http://localhost:8100/
${InvalidUser}      asd
${InvalidPassword}  asdasd
${ValidUser}        admin
${ValidPassword}    admin
${LoginTitle}       Login
${TasksTitle}       Tasks

*** Keywords ***
Open Browser To Login Page
   open browser    ${SiteUrl}    ${Browser}
   title should be     ${LoginTitle}

Invalid Login
    [Arguments]    ${username}    ${password}
    Input Username    ${username}
    Input User Password    ${password}
    Submit Credentials
    Login Should Have Failed
    
Valid Login
    [Arguments]    ${username}    ${password}
    Input Username    ${username}
    Input User Password    ${password}
    Submit Credentials
    Title Should Be    ${TasksTitle}

Input Username
    [Arguments]    ${username}
    Input Text    //input[@ng-model='data.username']    ${username}

Input User Password
    [Arguments]    ${password}
    Input Password    //input[@type='password']    ${password}
    
Submit Credentials
    Click Button    //button[contains(.,'Login')]

Login Should Have Failed
    Title Should Be      ${LoginTitle}
    Element Should Contain    //div[@class='popup']   unauthorized
    
Click Popup Ok
    Click Button      //button[contains(.,'OK')]
    
Click Qr Tab
    Click Element    //i[@class='icon ion-qr-scanner']

Click Logout Tab
    Click Element    //i[@class='icon ion-log-out']

Click Tasks Tab
    Click Element    //i[@class='icon ion-clipboard']

Click Logout Button
    Click Element    //button[@ng-click='logout()']


